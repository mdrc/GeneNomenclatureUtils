#!/usr/local/bin/perl
### nomenclature

=pod

=head1 NAME - pad_tabs_to_column_width

=head1 COMMAND LINE PARAMETERS 

Required parameters
  --file                    file to check
  --width                   width to which to pad (>=1)
    
=head1 DESCRIPTION

Pad the tab-delimited file to the specified width

=head1 AUTHOR - mike_croning@hotmail.com

=cut

use strict;
use warnings;
use Carp;
use GeneNomenclatureUtils::TabFileParser qw(
    parse_tab_delimited_file_to_array
    show_perldoc
);   
use Data::Dumper;
use Getopt::Long;

{
    print STDERR "\n$0\n", '=' x length($0), "\n\n";
    my ($file, $width);
        
    GetOptions(
        "file=s"          => \$file,
        "width=i"         => \$width
    ) or show_perldoc();
    show_perldoc("Must set --file") unless $file;
    unless ($width and $width > 0) {
        show_perldoc("Must set --width >= 0");
    }
    
    my $data = parse_tab_delimited_file_to_array($file, 'clean');

    my $line_count          = 0;
    my $modified_line_count = 0;
    foreach my $entry (@$data) {
        my $modified;

        $line_count++;
        while (@$entry < $width) {
            push(@$entry, '');
            $modified++;
        }
        
        $modified_line_count++ if $modified;
        print join("\t", @$entry);
		print "\n";        
    }
    
    print STDERR "Lines padded: $modified_line_count\n";    
    print STDERR "Total lines : $line_count\n\n";
} 
