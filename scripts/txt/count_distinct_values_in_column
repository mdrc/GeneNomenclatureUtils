#!/usr/local/bin/perl
### nomenclature

=pod

=head1 NAME - count_distinct_values_in_column

=head1 COMMAND LINE PARAMETERS 

Required parameters
  --file                    file to check
  --column                  column to analyse (>=1)
  
Optional parameters
  --skip_title              skip title row

=head1 DESCRIPTION

Counts the distinct values in the specified column of the tab-delimited
file, are reports counts of any duplicates.

=head1 AUTHOR - mike_croning@hotmail.com

=cut

use strict;
use warnings;
use Carp;
use GeneNomenclatureUtils::TabFileParser qw(
    parse_tab_delimited_file_to_array
    show_perldoc
);   
use Data::Dumper;
use Getopt::Long;

{
    print STDERR "\n$0\n", '=' x length($0), "\n\n";
    my ($file, $column_num, $skip_title);
    
    ### Command line parsing
    GetOptions(
        "file=s"      => \$file,
        "column=i"    => \$column_num,
        "skip_title"  => \$skip_title
    ) or show_perldoc();
    
    show_perldoc("Must set --file") unless $file;
    unless ($column_num and $column_num >= 1) {
        show_perldoc("Must set --column (>=1)");
    }

    my $data = parse_tab_delimited_file_to_array($file, 'clean');
    
    my $titles;
    if ($skip_title) {
        print STDERR "Skipping title row\n";
        $titles = shift(@$data);
        print join("\t", @$titles), "\n";
    }

    my $contents = {};
    print "\n";
    print "Column number: $column_num\n";
    print "Column title : '", $titles->[$column_num-1], "'\n" if $skip_title;
    
    foreach my $row (@$data) {
        my $content = $row->[$column_num-1];
        if (defined $content) {
            $contents->{$content}++ if defined $content;
        } else {
            $contents->{'_UNDEF_'}++;
        }
    }
    
   
    print "\nDistinct values: ", scalar(keys(%$contents)), "\n";
    
    print "\nDuplicates:\n";
    
    my $duplicate_count = 0;
    foreach my $key (keys(%$contents)) {
        if ($contents->{$key} > 1) {
            print "  $key", ' ' x (20 - length($key)), ' - ';
            print $contents->{$key}, "\n"; 
            
            $duplicate_count++;
        } 
    }
    
    print "\nTotal number of duplicates: $duplicate_count\n";
    
} 
