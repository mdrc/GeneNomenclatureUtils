#!/usr/local/bin/perl
### nomenclature

=pod

=head1 NAME - add_hgnc_id_by_hgnc_symbol

=head1 COMMAND LINE PARAMETERS

Required parameters
  --file                    file to be processed                
  --hgnc_symbol_column      column containing HGNC symbols (>= 1)
  --output_column           column to output the added IDs (>= 1)
  
Optional parameters
  --skip_title              skip first (title) row

=head1 DESCRIPTION

Adds HGNC IDs (eg. HGNC:4584) by the HGNC symbols found in the
specified column of a tab-delimited text file

Example command line

./add_hgnc_id_by_hgnc_symbol --file=my_file.txt --hgnc_symbol_column=1
--skip_title --output_column=1 > my_output.txt

This will prepend a column to the file with the requested IDs.

=head1 AUTHOR - mike_croning@hotmail.com

=cut 

use strict;
use warnings;
use Carp;
use GeneNomenclatureUtils::TabFileParser qw(
    show_perldoc
);
use GeneNomenclatureUtils::SeqIDNomenclature qw(
    check_or_translate_ids_in_file
    parse_hgnc_core_file
);
use Getopt::Long;
use Data::Dumper;

{
    print STDERR "\n$0\n", '=' x length($0), "\n\n";
    my ($file, $hgnc_symbol_column, $output_column, $skip_title);
        
    GetOptions(
        "file=s"                => \$file,
        "hgnc_symbol_column=i"  => \$hgnc_symbol_column,
        "output_column=i"       => \$output_column,
        "skip_title"            => \$skip_title,
    ) or show_perldoc();
    show_perldoc("Must set --file") unless $file;
    unless ($hgnc_symbol_column and $hgnc_symbol_column >= 1) {
        show_perldoc("Must set --hgnc_symbol_column >= 1");
    }
    unless ($output_column and $output_column >= 1) {
        show_perldoc("Must set --output_column >= 1");
    }

    ### Parse nomenclature file(s)
    my ($parsed_by_symbol, $parsed_by_hgnc_id, $approved_name_by_hgnc_id)
        = parse_hgnc_core_file();

    ### Do the translation
    check_or_translate_ids_in_file($file, $parsed_by_symbol, 'HGNC ID'
        , '^\w+$', $hgnc_symbol_column, $output_column, $skip_title, 'translate');
}
